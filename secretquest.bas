1 MODE 1:LINE INPUT"Make sure CapsLock is active:";A$
2 REM Secret Quest 2010 By Eric Iwasaki
3 REM 107 Via Pasqual
4 REM Redondo Beach, CA 90277
5 REM All graphics created on DrawPlus
6 REM and then were converted into
7 REM smaller images to conserve memory
9 REM Converted to Locomotive Basic for CPC Dec. 2016 by SRS
10 ROOM=1
11 DISK=0
12 LAMP=0
13 FOOD=0
14 CODE=0
15 ROOMMATE=0
16 GUN=0
17 BATTERY=0
18 BRIDGE=0
19 SABER=0
20 CARD=0
30 MODE 1:CLS
40 REM
50 LOCATE 25,1:PRINT"             Hit any key>";:LOCATE 1,1
51 A$=INKEY$:IF A$="" THEN 51
60 CLS
70 INPUT"Do you want instructions";A$
80 IF LEFT$(A$,1)="Y" OR LEFT$(A$,1)="y" THEN 90
85 GOTO 191
90 CLS
110 PRINT"     You and your roommate have been "
120 PRINT" traveling through space for several "
130 PRINT" years on a cruise to planet Espion and"
140 PRINT" back to planet Earth."
150 PRINT"     Your cruise is now almost over and"
160 PRINT" your ship, the Voyager, is now orbiting";
170 PRINT" the Earth.  Right now you and your room";
180 PRINT" mate are fast asleep..."
181 PRINT"     All commands are given in 1 or 2"
182 PRINT" word format.  To list all of the "
183 PRINT" commands, type HELP.  Also, type LOOK"
184 PRINT" in each room because sometimes it gives";
185 PRINT" important hints required for survival.":PRINT" HINT: Use it everytime you enter the":PRINT" Tractor Beam Power Core!"
186 PRINT"     At the UPPER-RIGHT corner of the"
187 PRINT" screen, the visible and usable objects"
188 PRINT" are listed.  You can USE usable objects";
189 PRINT" and you can GET visible objects.":PRINT" Be sure to look at them after you LOOK":PRINT" in a room or KILL a creature."
190 PRINT" To exit the ship, use the Transporter.":PRINT:LINE INPUT"              Hit <Return>";A$
191 CLS
192 REM
193 LOCATE 16,1:PRINT"     You suddenly wake up.  You hear"
194 PRINT" a scream and then a door open.":PRINT
195 LINE INPUT" What's your roommate's name>";ROOMMATE$
196 REM
197 PRINT:PRINT" You turn on the lights and realize that";
198 PRINT" ";ROOMMATE$;" is gone!":FOR I=1 TO 2000:NEXT I
199 LOOK=1:ROOM=1:GOTO 203
200 LOOK=1:ROOM=1
201 REM
203 LOCATE 16,1:PRINT"     You are in your quarters.  There is";
204 PRINT" an exit to the north."
210 IF DISK=0 AND LOOK=0 THEN LOCATE 5,25:PRINT"Coordinate Disk"
211 IF DISK=1 THEN LOCATE 5,25:PRINT"               "
220 GOSUB 10000
230 IF LEFT$(COMMAND$,2)="LO" AND DISK=1 THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
231 IF LEFT$(COMMAND$,2)="LO" AND DISK=0 THEN LOOK=0:LOCATE 21,2:PRINT" You spot a disk on the computer."
239 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 299
240 IF LEFT$(COMMAND$,1)="N" THEN 300
241 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
299 GOTO 210
300 LOOK=1:ROOM=2:CLS
301 REM
303 LOCATE 16,1:PRINT"     You are at the end of a short hall-";
304 PRINT" way.  There are exits to the north end"
305 PRINT" and the south end of the hall."
310 REM
320 GOSUB 10000
330 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT"There's a sign that reads`Ship Central'";
339 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 399
340 IF LEFT$(COMMAND$,1)="N" THEN 400
341 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
342 IF LEFT$(COMMAND$,1)="S" THEN CLS:GOTO 200
399 GOTO 310
400 LOOK=1:ROOM=3:CLS
401 REM
403 LOCATE 16,1:PRINT"     You are at the center of the cruise";
404 PRINT" ship.  In the center of the room lies"
405 PRINT" an elevator.  An exit is to the south.";
410 LOCATE 11,25:PRINT"Elevator"
420 GOSUB 10000
430 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT"There's a sign that reads`Level 1'";
439 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 499
440 IF LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
441 IF LEFT$(COMMAND$,1)="S" THEN GOTO 300
499 GOTO 410
500 LOOK=1:ROOM=4:CLS
501 REM
503 LOCATE 16,1:PRINT"     You are at the center of the cruise";
504 PRINT" ship.  In the center of the room lies"
505 PRINT" an elevator.  Exits to the N,W, and E.";
510 LOCATE 11,25:PRINT"Elevator"
520 GOSUB 10000
530 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT"There's a sign that reads`Level 2'";
539 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 599
540 IF LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" OR LEFT$(COMMAND$,1)="S" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
541 IF LEFT$(COMMAND$,1)="W" THEN GOTO 2300
542 IF LEFT$(COMMAND$,1)="E" THEN GOTO 600
543 IF LEFT$(COMMAND$,1)="N" THEN GOTO 700
599 GOTO 510
600 LOOK=1:ROOM=5:CLS
601 REM
603 LOCATE 16,1:PRINT"     You are in the dining room.  There"
604 PRINT" is food on a rack.  The only exit is to";
605 PRINT" the west."
610 IF FOOD=0 THEN LOCATE 5,25:PRINT"Food"
611 IF FOOD=1 THEN LOCATE 5,25:PRINT"               "
620 GOSUB 10000
630 IF LEFT$(COMMAND$,2)="LO" AND FOOD>0 THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
631 IF LEFT$(COMMAND$,2)="LO" AND FOOD=0 THEN LOOK=0:LOCATE 21,2:PRINT" There is some food here."
639 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 699
640 IF LEFT$(COMMAND$,1)="W" THEN 500
641 IF LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
699 GOTO 610
700 LOOK=1:ROOM=6:CLS
701 REM
703 LOCATE 16,1:PRINT"     You are in the ship's rest room."
704 PRINT" There is a toilet in this room.  There"
705 PRINT" is an exit to the south."
710 LOCATE 11,25:PRINT"Toilet"
720 GOSUB 10000
730 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" There is toilet here."
739 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 799
740 IF LEFT$(COMMAND$,1)="S" THEN 500
741 IF LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
799 GOTO 710
800 LOOK=1:ROOM=7:CLS
801 REM
803 LOCATE 16,1:PRINT"     You are at the center of the cruise";
804 PRINT" ship.  In the center of the room lies"
805 PRINT" an elevator.  Exits in all directions.";
810 LOCATE 11,25:PRINT"Elevator"
820 GOSUB 10000
830 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT"There's a sign that reads`Level 3'";
839 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 899
840 IF LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
841 IF LEFT$(COMMAND$,1)="W" THEN GOTO 2000
842 IF LEFT$(COMMAND$,1)="E" THEN GOTO 900
843 IF LEFT$(COMMAND$,1)="S" THEN GOTO 1000
844 IF LEFT$(COMMAND$,1)="N" THEN GOTO 1200
899 GOTO 810
900 LOOK=1:ROOM=8:CLS
901 REM
903 LOCATE 16,1:PRINT"     You are in the weapon room.  There"
904 PRINT" are machine guns behind a force field."
905 PRINT" The exit is to the west."
910 IF GUN=0 AND LOOK=0 THEN LOCATE 5,25:PRINT"Laser Gun"
911 IF GUN=1 THEN LOCATE 5,25:PRINT"               "
920 GOSUB 10000
930 IF LEFT$(COMMAND$,2)="LO" AND GUN=1 THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
931 IF LEFT$(COMMAND$,2)="LO" AND GUN=0 THEN LOOK=0:LOCATE 21,2:PRINT" There is a gun attached to the table!"
939 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 999
940 IF LEFT$(COMMAND$,1)="W" THEN 800
941 IF LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
999 GOTO 910
1000 LOOK=1:ROOM=9:CLS
1001 REM
1003 LOCATE 16,1:PRINT"     You are in the engine room.  You"
1004 PRINT" can see the Reactor to the south and"
1005 PRINT" an exit to the north."
1010 IF BATTERY=0 THEN LOCATE 5,25:PRINT"Battery"
1011 IF BATTERY=1 THEN LOCATE 5,25:PRINT"               "
1020 GOSUB 10000
1031 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" The Reactor is to the south."
1039 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1099
1040 IF LEFT$(COMMAND$,1)="N" THEN 800
1041 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1042 IF LEFT$(COMMAND$,1)="S" THEN 1100
1099 GOTO 1010
1100 LOOK=1:ROOM=10:CLS
1101 REM
1110 LOCATE 16,1:PRINT"     You are so stupid!  Don't you know"
1120 PRINT" that the reactors are too hot for"
1130 PRINT" humans to survive in!!"
1131 PRINT" ***You have DIED!***"
1140 PRINT:INPUT" Play again (Y/N)";A$
1150 IF A$="N" THEN CLS:END
1151 IF A$="Y" THEN CLS:RUN
1160 GOTO 1140
1200 LOOK=1:ROOM=11:CLS
1201 REM
1203 LOCATE 16,1:PRINT"     You are in a long hallway.  There"
1204 PRINT" is an exit to the south and the hall"
1205 PRINT" continues to the north."
1210 REM
1220 GOSUB 10000
1230 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
1239 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1299
1240 IF LEFT$(COMMAND$,1)="N" THEN 1300
1241 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1242 IF LEFT$(COMMAND$,1)="S" THEN 800
1299 GOTO 1210
1300 LOOK=1:ROOM=12:CLS
1301 REM
1303 LOCATE 16,1:PRINT"     You are in a long hallway.  The"
1304 PRINT" hall continues to both the north and "
1305 PRINT" the south."
1310 REM
1320 GOSUB 10000
1330 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
1339 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1399
1340 IF LEFT$(COMMAND$,1)="N" THEN 1400
1341 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1342 IF LEFT$(COMMAND$,1)="S" THEN 1200
1399 GOTO 1310
1400 LOOK=1:ROOM=13:IF DEAD=0 THEN CLS
1401 LOOK=1:ROOM=13:IF DEAD>0 THEN CLS
1402 SMOVE=BEWE:REM
1403 LOCATE 16,1:PRINT"     You are in a long hallway.  A"
1404 IF DEAD=0 THEN PRINT" guard stands here.  Stairs go down and"
1405 IF DEAD>0 THEN PRINT" guard lies here.  Stairs go down and"
1406 PRINT" the hall leads to the N and S."
1410 IF DEAD>=1 AND BRIDGE=0 THEN LOCATE 5,25:PRINT"Key"
1411 IF DEAD>=1 AND BRIDGE=1 THEN LOCATE 5,25:PRINT"               "
1420 GOSUB 10000
1430 IF LEFT$(COMMAND$,2)="LO" AND BRIDGE=0 THEN LOOK=0:LOCATE 21,2:PRINT" The guard has a key."
1431 IF LEFT$(COMMAND$,2)="LO" AND BRIDGE=1 THEN LOOK=0:LOCATE 21,2:PRINT" There is a dead guard here."
1439 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1499
1440 IF LEFT$(COMMAND$,1)="S" THEN 1300
1441 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1442 IF DEAD>0 AND LEFT$(COMMAND$,1)="N" THEN 1600
1443 IF DEAD>0 AND LEFT$(COMMAND$,1)="D" THEN 1500
1499 GOTO 1410
1500 LOOK=1:ROOM=14:CLS
1501 REM
1503 LOCATE 16,1:PRINT"     You are in a supply room.  The"
1504 PRINT" stair case leads up.  There are no "
1505 PRINT" other exits."
1510 IF LAMP=0 THEN LOCATE 5,25:PRINT"Lamp"
1511 IF LAMP=1 THEN LOCATE 5,25:PRINT"               "
1520 GOSUB 10000
1530 IF LEFT$(COMMAND$,2)="LO" AND LAMP=0 THEN LOOK=0:LOCATE 21,2:PRINT" There is a lamp here."
1531 IF LEFT$(COMMAND$,2)="LO" AND LAMP=1 THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
1539 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1599
1540 IF LEFT$(COMMAND$,1)="U" THEN 1400
1541 IF LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1599 GOTO 1510
1600 LOOK=1:ROOM=15:CLS
1601 REM
1603 LOCATE 16,1:PRINT"     You are in a long hallway.  The"
1604 PRINT" hall continues to both the north and "
1605 PRINT" the south."
1610 REM
1620 GOSUB 10000
1630 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
1639 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1699
1640 IF LEFT$(COMMAND$,1)="N" THEN 1700
1641 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1642 IF LEFT$(COMMAND$,1)="S" THEN 1400
1699 GOTO 1610
1700 LOOK=1:ROOM=16:CLS
1701 REM
1703 LOCATE 16,1:PRINT"     You are in a long hallway.  The"
1704 PRINT" hall continues to the south and an exit";
1705 PRINT" is to the north."
1710 REM
1720 GOSUB 10000
1730 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" There is a diagram of the ship."
1739 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1799
1740 IF LEFT$(COMMAND$,1)="N" THEN 1800
1741 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1742 IF LEFT$(COMMAND$,1)="S" THEN 1600
1799 GOTO 1710
1800 LOOK=1:ROOM=17:IF DEAD=1 THEN CLS
1801 LOOK=1:ROOM=17:IF DEAD>=2 THEN CLS
1802 SMOVE=BEWE:REM
1803 LOCATE 16,1:PRINT"     You are at the front of the space"
1804 IF DEAD=1 THEN PRINT" cruiser.  A guard stands here.  Stairs"
1805 IF DEAD>1 THEN PRINT" cruiser.  A guard lies here.  Stairs"
1806 PRINT" go up.  An exit is to the south."
1810 IF DEAD=2 AND SABER=0 THEN LOCATE 5,25:PRINT"Light Saber"
1811 IF DEAD=2 AND SABER=1 THEN LOCATE 5,25:PRINT"               "
1820 GOSUB 10000
1830 IF LEFT$(COMMAND$,2)="LO" AND SABER=0 THEN LOOK=0:LOCATE 21,2:PRINT" The guard has a light saber."
1831 IF LEFT$(COMMAND$,2)="LO" AND SABER=1 THEN LOOK=0:LOCATE 21,2:PRINT" There is a dead guard here."
1839 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1899
1840 IF LEFT$(COMMAND$,1)="S" THEN 1700
1841 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1842 IF DEAD>=2 AND BRIDGE=1 AND LEFT$(COMMAND$,1)="U" THEN 1900
1843 IF DEAD>=2 AND BRIDGE=0 AND LEFT$(COMMAND$,1)="U" THEN LOCATE 21,2:PRINT"You need a key."
1899 GOTO 1810
1900 LOOK=1:ROOM=18:CLS
1901 REM
1903 LOCATE 16,1:PRINT"     You are on the bridge of the space"
1904 PRINT" cruiser.  The captain sits in his chair";
1905 PRINT" and the stairs go down."
1910 IF CARD=0 THEN LOCATE 5,25:PRINT"Card"
1911 IF CARD=1 THEN LOCATE 5,25:PRINT"               "
1920 GOSUB 10000
1930 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" The captain is in his chair."
1939 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 1999
1940 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="S" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
1942 IF LEFT$(COMMAND$,1)="D" THEN 1800
1999 GOTO 1910
2000 LOOK=1:ROOM=19:CLS
2002 SMOVE=BEWE:REM
2003 LOCATE 16,1:PRINT"     You are in the tractor beam power "
2004 PRINT" core.  There is an exit to the east"
2005 PRINT" and a ladder leads down."
2010 REM
2020 GOSUB 10000
2030 IF LEFT$(COMMAND$,2)="LO" AND BATTERY=1 AND GUN=1 THEN LOOK=0:LOCATE 21,2:PRINT"You notice your gun and battery move.":GOTO 2039
2031 IF LEFT$(COMMAND$,2)="LO" AND BATTERY=0 AND GUN=1 THEN LOOK=0:LOCATE 21,2:PRINT"You notice your gun move.":GOTO 2039
2032 IF LEFT$(COMMAND$,2)="LO" AND BATTERY=1 AND GUN=0 THEN LOOK=0:LOCATE 21,2:PRINT"You notice your battery move.":GOTO 2039
2039 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 2099
2040 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="U" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
2041 IF LEFT$(COMMAND$,1)="D" AND GUN=1 THEN 63000
2042 IF LEFT$(COMMAND$,1)="D" AND BATTERY=1 THEN 63000
2043 IF LEFT$(COMMAND$,1)="E" THEN 800
2044 IF LEFT$(COMMAND$,1)="D" AND GUN=0 AND BATTERY=0 THEN 2100
2099 GOTO 2010
2100 LOOK=1:ROOM=20:IF OPN=0 THEN CLS
2101 LOOK=1:ROOM=20:IF OPN>0 THEN CLS
2102 SMOVE=BEWE:REM
2103 LOCATE 16,1:PRINT"     You are in a fairly big room.  A"
2104 IF OPN=0 THEN PRINT" locked door is to the east.  A ladder"
2105 IF OPN>0 THEN PRINT" door lies to the east.  A ladder"
2106 PRINT" leads to the tractor beam room."
2110 IF OPN=0 AND CARD=1 THEN LOCATE 11,25:PRINT"Card"
2111 IF OPN>0 THEN LOCATE 11,25:PRINT"    "
2120 GOSUB 10000
2130 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" You notice a slot by the door."
2139 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 2199
2140 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
2141 IF LEFT$(COMMAND$,1)="E" AND OPN=0 THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"The door is locked."
2142 IF LEFT$(COMMAND$,1)="E" AND OPN>0 AND LAMP=1 THEN 2200
2143 IF LEFT$(COMMAND$,1)="U" THEN 2000
2144 IF LEFT$(COMMAND$,1)="E" AND OPN>0 AND LAMP=1 THEN 58000
2199 GOTO 2110
2200 LOOK=1:ROOM=21:IF DEAD=2 THEN CLS
2201 LOOK=1:ROOM=21:IF DEAD>=3 AND OPN<2 THEN CLS
2202 LOOK=1:ROOM=21:IF DEAD>=3 AND OPN>1 THEN CLS
2203 SMOVE=BEWE:REM
2204 LOCATE 16,1:PRINT"     You are in a fairly big room.  A"
2205 IF OPN=1 THEN PRINT" locked cell is to the east.  An open"
2206 IF OPN>1 THEN PRINT" cell lies to the east.  An open"
2207 PRINT" door leads to the west."
2208 IF DEAD<3 THEN PRINT" THE ALIEN IS HERE!"
2209 IF DEAD=3 THEN PRINT" The alien is DEAD!"
2210 IF DEAD=3 AND OPN<2 THEN LOCATE 11,25:PRINT"Button"
2211 IF DEAD=3 AND OPN>1 THEN LOCATE 11,25:PRINT"      "
2212 IF DEAD=3 AND CODE=0 THEN LOCATE 5,25:PRINT"Access Code"
2213 IF DEAD=3 AND CODE=1 THEN LOCATE 5,25:PRINT"               "
2214 IF DEAD=3 AND ROOMMATE=0 AND OPN>1 THEN LOCATE 6,25:PRINT"Roommate"
2215 IF DEAD=3 AND ROOMMATE=1 AND OPN>1 THEN LOCATE 6,25:PRINT"               "
2220 GOSUB 10000
2230 IF LEFT$(COMMAND$,2)="LO" AND OPN<2 THEN LOOK=0:LOCATE 21,2:PRINT" You notice a button on the wall."
2239 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 2299
2240 IF LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="D" OR LEFT$(COMMAND$,1)="U" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
2242 IF LEFT$(COMMAND$,1)="W" THEN 2100
2299 GOTO 2210
2300 LOOK=1:ROOM=22:CLS
2301 REM
2303 LOCATE 16,1:PRINT"     You are in a short hallway.  There"
2304 PRINT" is an exit to the east and the hall"
2305 PRINT" continues to the north."
2310 REM
2320 GOSUB 10000
2330 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
2339 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 2399
2340 IF LEFT$(COMMAND$,1)="N" THEN 2400
2341 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
2342 IF LEFT$(COMMAND$,1)="E" THEN 500
2399 GOTO 2310
2400 LOOK=1:ROOM=23:CLS
2401 REM
2403 LOCATE 16,1:PRINT"     You are in a short hallway.  There"
2404 PRINT" is an exit to the north and the hall"
2405 PRINT" continues to the south."
2410 REM
2420 GOSUB 10000
2430 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" There is nothing here."
2439 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 2499
2440 IF LEFT$(COMMAND$,1)="N" THEN 2500
2441 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
2442 IF LEFT$(COMMAND$,1)="S" THEN 2300
2499 GOTO 2410
2500 LOOK=1:ROOM=24:CLS
2501 REM
2503 LOCATE 16,1:PRINT"     You are in the ships molecular"
2504 PRINT" transportation room.  The only exit"
2505 PRINT" is to the south."
2510 LOCATE 11,25:PRINT"Transporter"
2520 GOSUB 10000
2530 IF LEFT$(COMMAND$,2)="LO" THEN LOOK=0:LOCATE 21,2:PRINT" The transporter is here."
2539 IF LEFT$(COMMAND$,3)="EAT" OR LEFT$(COMMAND$,3)="DRO" OR LEFT$(COMMAND$,3)="USE" THEN 2599
2540 IF LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't go that way."
2541 IF LEFT$(COMMAND$,1)="S" THEN 2400
2599 GOTO 2510
9999 END
10000 BEWE=BEWE+1:LOCATE 20,2:PRINT">";COMMAND$
10001 LOCATE 3,25:PRINT"Visible Objects"
10002 LOCATE 9,25:PRINT"Usable Objects"
10007 IF DEAD>=2 AND DEAD<3 AND ROOM=21 AND SMOVE<BEWE+1 THEN 61100
10008 IF DEAD=0 AND ROOM=13 AND SMOVE<BEWE+1 THEN 61000
10009 IF DEAD>=1 AND DEAD<2 AND ROOM=17 AND SMOVE<BEWE+1 THEN 61000
10010 LOCATE 23,2:LINE INPUT">";COMMAND$:LOCATE 21,2
10011 LOCATE 20,3:PRINT"                                     ";
10012 LOCATE 20,2:PRINT">";COMMAND$
10013 LOCATE 21,2:PRINT"                                       ";
10014 LOCATE 23,3:PRINT"                                     ";
10020 IF LEFT$(COMMAND$,3)="GET" THEN 11000
10021 IF LEFT$(COMMAND$,3)="EAT" THEN 11500
10030 IF LEFT$(COMMAND$,3)="DRO" THEN 11100
10040 IF LEFT$(COMMAND$,3)="USE" THEN 11200
10050 IF LEFT$(COMMAND$,3)="KIL" THEN 11300
10060 IF LEFT$(COMMAND$,1)="I" THEN 11400
10061 IF LEFT$(COMMAND$,3)="QUI" THEN CLS:END
10070 IF LEFT$(COMMAND$,3)="ASS" THEN LOCATE 21,2:PRINT"Such horrible language!":RETURN
10075 IF COMMAND$="HELP" THEN 50000
10080 IF COMMAND$="FUCK "+ROOMMATE$ AND ROOMMATE=1 THEN LOCATE 21,2:PRINT"Uh! Uh! That feels so good!":RETURN
10090 IF COMMAND$="FUCK YOU" THEN LOCATE 21,2:PRINT"Same to you ASSHOLE!":RETURN
10100 IF COMMAND$="FUCK YOU ASSHOLE" THEN LOCATE 21,2:PRINT"You're not the Terminator!":RETURN
10110 IF COMMAND$="FART" THEN LOCATE 21,2:PRINT"Ahhhhh! That smells!":RETURN
10120 IF COMMAND$="BURP" THEN LOCATE 21,2:PRINT"Belch!":RETURN
10130 IF COMMAND$="HI" THEN LOCATE 21,2:PRINT"Buy Reflex Point! (Disk 487) By E.I.":RETURN
10131 IF COMMAND$="FUCK "+ROOMMATE$ AND ROOMMATE=0 THEN LOCATE 21,2:PRINT"You don't have "+ROOMMATE$+" yet.":RETURN
10132 IF COMMAND$="KISS "+ROOMMATE$ AND ROOMMATE=1 THEN LOCATE 21,2:PRINT"You give ";ROOMMATE$;" a kiss.":RETURN
10133 IF COMMAND$="KISS "+ROOMMATE$ AND ROOMMATE=0 THEN LOCATE 21,2:PRINT"You don't have "+ROOMMATE$+" yet.":RETURN
10134 IF LEFT$(COMMAND$,4)="FUCK" THEN LOCATE 21,2:PRINT"Wash your mouth out with soap!":RETURN
10140 IF LEFT$(COMMAND$,6)="PC-SIG" THEN LOCATE 21,2:PRINT"Great public domain software!":RETURN
10150 IF LEFT$(COMMAND$,1)="W" OR LEFT$(COMMAND$,1)="E" OR LEFT$(COMMAND$,1)="S" OR LEFT$(COMMAND$,1)="U" OR LEFT$(COMMAND$,1)="D" OR LEFT$(COMMAND$,1)="N" OR LEFT$(COMMAND$,2)="LO" THEN RETURN
10999 SOUND 3,400,2,10:LOCATE 21,2:PRINT"What?":RETURN
11000 IF RIGHT$(COMMAND$,2)="SK" AND DISK=0 AND ROOM=1 THEN DISK=1:LOCATE 21,2:PRINT"You got the disk.":RETURN
11010 IF RIGHT$(COMMAND$,2)="MP" AND LAMP=0 AND ROOM=14 THEN LAMP=1:LOCATE 21,2:PRINT"You got the lamp.":RETURN
11020 IF RIGHT$(COMMAND$,2)="OD" AND FOOD=0 AND ROOM=5 THEN FOOD=1:LOCATE 21,2:PRINT"You got the food.":RETURN
11030 IF RIGHT$(COMMAND$,2)="DE" AND CODE=0 AND ROOM=21 THEN CODE=1:LOCATE 21,2:PRINT"You got the access code.":RETURN
11040 IF RIGHT$(COMMAND$,2)="TE" AND ROOMMATE=0 AND ROOM=21 AND DEAD=3 AND OPN>1 THEN ROOMMATE=1:LOCATE 21,2:PRINT"You have rescued ";ROOMMATE$;".":RETURN
11050 IF RIGHT$(COMMAND$,2)="UN" AND GUN=0 AND ROOM=8 AND EATEN=1 THEN GUN=1:LOCATE 21,2:PRINT"With a hard pull, you remove the gun.":RETURN
11051 IF RIGHT$(COMMAND$,2)="UN" AND GUN=0 AND ROOM=8 AND EATEN=0 THEN LOCATE 21,2:PRINT"You're not strong enough to get it.":RETURN
11060 IF RIGHT$(COMMAND$,2)="RY" AND BATTERY=0 AND ROOM=9 THEN BATTERY=1:LOCATE 21,2:PRINT"You got the battery.":RETURN
11070 IF RIGHT$(COMMAND$,2)="EY" AND BRIDGE=0 AND ROOM=13 AND DEAD>=1 THEN BRIDGE=1:LOCATE 21,2:PRINT"You got the bridge key.":RETURN
11080 IF RIGHT$(COMMAND$,3)="BER" AND SABER=0 AND ROOM=17 AND DEAD>=2 THEN SABER=1:LOCATE 21,2:PRINT"You got the light saber.":RETURN
11090 IF RIGHT$(COMMAND$,3)="ARD" AND CARD=0 AND ROOM=18 THEN CARD=1:LOCATE 21,2:PRINT"You got the cardlock card.":RETURN
11099 LOCATE 21,2:SOUND 3,400,2,10:PRINT"I don't see that here.":RETURN
11100 IF RIGHT$(COMMAND$,2)="RY" AND BATTERY=1 THEN BATTERY=0:LOCATE 21,2:PRINT"You've dropped the battery.":RETURN
11110 IF RIGHT$(COMMAND$,2)="UN" AND GUN=1 THEN GUN=0:LOCATE 21,2:PRINT"You've dropped the laser gun.":RETURN
11199 LOCATE 21,2:SOUND 3,400,2,10:PRINT"You can't drop that.":RETURN
11200 IF RIGHT$(COMMAND$,2)="ER" AND ROOM=24 AND DISK=1 AND CODE=1 AND NEEDTOGO=0 THEN CLS:GOTO 59000
11201 IF RIGHT$(COMMAND$,2)="ER" AND ROOM=24 AND DISK=0 AND CODE=1 AND NEEDTOGO=0 THEN CLS:GOTO 57000
11202 IF RIGHT$(COMMAND$,2)="ER" AND ROOM=24 AND DISK=1 AND CODE=1 AND NEEDTOGO=1 THEN CLS:GOTO 56000
11210 IF RIGHT$(COMMAND$,2)="OR" AND ROOM=3 OR ROOM=4 OR ROOM=7 THEN CLS:GOTO 60000
11220 IF RIGHT$(COMMAND$,2)="ET" AND ROOM=6 AND NEEDTOGO=1 THEN LOCATE 21,2:PRINT"Ahhhhh!":NEEDTOGO=0:RETURN
11230 IF RIGHT$(COMMAND$,2)="RD" AND ROOM=20 AND CARD=1 THEN CARD=0:OPN=1:GOTO 2100
11240 IF RIGHT$(COMMAND$,2)="ON" AND ROOM=21 AND DEAD=3 AND OPN<2 THEN OPN=2:GOTO 2200
11299 SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't use that, at least not now!":RETURN
11300 REM
11301 IF GUN=1 AND BATTERY=1 AND RIGHT$(COMMAND$,2)="RD" AND ROOM=13 AND DEAD=0 THEN DEAD=1:GOTO 1400
11302 IF GUN=1 AND BATTERY=0 AND RIGHT$(COMMAND$,2)="RD" AND ROOM=13 AND DEAD=0 THEN LOCATE 21,2:PRINT"Your gun doesn't work!":RETURN
11310 IF GUN=1 AND BATTERY=1 AND RIGHT$(COMMAND$,2)="RD" AND ROOM=17 AND DEAD=1 THEN DEAD=1.5:LOCATE 21,2:PRINT"You hit him!":FOR I=1 TO 1000:NEXT I:RETURN
11311 IF GUN=1 AND BATTERY=0 AND RIGHT$(COMMAND$,2)="RD" AND ROOM=17 AND DEAD=1 THEN LOCATE 21,2:PRINT"Your gun doesn't work!":RETURN
11312 IF GUN=1 AND BATTERY=1 AND RIGHT$(COMMAND$,2)="RD" AND ROOM=17 AND DEAD=1.5 THEN DEAD=2:GOTO 1800
11313 IF RIGHT$(COMMAND$,2)="IN" AND ROOM=18 THEN HIT=6:GOTO 61000
11320 IF SABER=1 AND RIGHT$(COMMAND$,2)="EN" AND ROOM=21 AND DEAD=2 THEN DEAD=2.5:LOCATE 21,2:PRINT"You hit him!":FOR I=1 TO 1000:NEXT I:RETURN
11321 IF SABER=1 AND RIGHT$(COMMAND$,2)="EN" AND ROOM=21 AND DEAD=2.5 THEN DEAD=2.75:LOCATE 21,2:PRINT"You hit him!":FOR I=1 TO 1000:NEXT I:RETURN
11322 IF SABER=1 AND RIGHT$(COMMAND$,2)="EN" AND ROOM=21 AND DEAD=2.75 THEN DEAD=3:GOTO 2200
11323 IF ROOMMATE=1 AND MID$(COMMAND$,6)=ROOMMATE$ THEN LOCATE 21,2:PRINT"Are you out of your GOURD?!?!":RETURN
11330 IF GUN=0 AND SABER=0 THEN LOCATE 21,2:PRINT"You have no weapon!":RETURN
11399 SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't kill that, at least not now!":RETURN
11400 CLS:PRINT"You are carrying:"
11410 PRINT
11420 IF DISK=1 THEN PRINT"Transporter Coordinate Disk"
11430 IF LAMP=1 THEN PRINT"Lamp"
11440 IF FOOD=1 THEN PRINT"Food"
11450 IF CODE=1 THEN PRINT"Transporter Access Code"
11460 IF ROOMMATE=1 THEN PRINT ROOMMATE$
11470 IF GUN=1 THEN PRINT"Laser Gun"
11480 IF BATTERY=1 THEN PRINT"Laser Battery"
11490 IF BRIDGE=1 THEN PRINT"Bridge Key"
11491 IF SABER=1 THEN PRINT"Light Saber"
11492 IF CARD=1 THEN PRINT"Cardlock Card"
11493 IF DISK=0 AND LAMP=0 AND FOOD<>1 AND CODE=0 AND ROOMMATE=0 AND GUN=0 AND BATTERY=0 AND BRIDGE=0 AND SABER=0 AND CARD=0 THEN PRINT"Nothing"
11494 PRINT:PRINT"Hit any key>"
11495 A$=INKEY$:IF A$="" THEN 11495
11496 CLS:REM:RETURN
11500 IF RIGHT$(COMMAND$,4)="FOOD" AND FOOD=1 THEN FOOD=2:NEEDTOGO=1:EATEN=1:LOCATE 21,2:PRINT"You feel stronger!":RETURN
11510 IF MID$(COMMAND$,5)=ROOMMATE$ THEN LOCATE 21,2:PRINT"That's gross!":RETURN
11599 SOUND 3,400,2,10:LOCATE 21,2:PRINT"You can't eat that, at least not now!":RETURN
50000 CLS
50010 PRINT"List of commands:"
50020 PRINT
50030 PRINT" LOOK"
50040 PRINT" GET"
50050 PRINT" DROP"
50060 PRINT" USE"
50070 PRINT" QUIT"
50080 PRINT" I or INVENTORY"
50090 PRINT" N or NORTH"
50100 PRINT" S or SOUTH"
50110 PRINT" W or WEST"
50120 PRINT" E or EAST"
50121 PRINT" U or UP"
50122 PRINT" D or DOWN"
50130 PRINT" EAT"
50140 PRINT" KILL"
50150 PRINT
50160 PRINT"NOTE: There are interesting responses"
50170 PRINT"to certain words or phrases that are"
50180 PRINT"not listed here.  Those words and "
50190 PRINT"phrases don't start with N,S,W,E,U,or D.";
50191 PRINT:PRINT"Hit any key>"
50192 A$=INKEY$:IF A$="" THEN 50192
50193 CLS:REM:RETURN
50999 END
56000 CLS
56010 PRINT"As you enter the transporter, you slowly";
56020 PRINT"decompose your molecules.  Suddenly"
56030 PRINT"you have to go to the bathroom and "
56040 PRINT"solid wastes begin to leave you rear."
56050 PRINT"Since your molecules are changing their"
56060 PRINT"composition, when you recompose them,"
56070 PRINT"you BLOW UP!"
56080 PRINT"***You have DIED!***"
56090 GOTO 62000
57000 CLS
57010 PRINT"As you enter the transporter, you slowly";
57020 PRINT"decompose and then recompose your "
57030 PRINT"molecules.  Before you know it, you're"
57040 PRINT"floating in outer space."
57050 PRINT"***You have DIED!***"
57060 GOTO 62000
58000 CLS
58010 LOCATE 16,1:PRINT"     You enter an extremely dark room."
58020 PRINT" You hear a breathing noise and then "
58030 PRINT" you feel incredibly hot... "
58040 PRINT" ***You have DIED!***"
58050 GOTO 62000
59000 CLS
59010 PRINT"As you enter the transporter, you slowly";
59020 PRINT"decompose and then recompose your "
59030 PRINT"molecules.  Before you know it, you're"
59040 PRINT"on the door step to your house.
59041 IF ROOMMATE=0 THEN 59100
59050 PRINT"    ";ROOMMATE$;" thanks you dearly for"
59060 PRINT"rescuing him/her.  You have completed"
59070 PRINT"the Quest in";BEWE;" moves."
59080 PRINT"The least amount of moves is 59"
59090 GOTO 62000
59100 PRINT"    You have returned to Earth pre-"
59110 PRINT"maturly for you left your roommate"
59120 PRINT"on board the space cruiser."
59130 PRINT"In the following years to come you miss"
59140 PRINT ROOMMATE$;" dearly."
59150 GOTO 62000
60000 PRINT"There are three buttons on the wall."
60010 PRINT:INPUT"Push which button (1,2,3)";A
60015 REM
60020 ON A GOTO 400,500,800
61000 REM FIRE ROUTINE
61010 IF RND(1)*100>75 THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"He fired and Hit!                     ":HIT=HIT+1
61011 IF ROOM=18 THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"He fired and Hit!                     ":HIT=HIT+1
61020 IF HIT>4 THEN LOCATE 21,2:PRINT"***You have DIED!***                  ":GOTO 62000
61030 GOTO 10010
61100 REM FIRE ROUTINE
61110 IF RND(1)*100>25 THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"He fired and Hit!                     ":HIT=HIT+1
61111 IF ROOM=18 THEN SOUND 3,400,2,10:LOCATE 21,2:PRINT"He fired and Hit!                     ":HIT=HIT+1
61120 IF HIT>4 THEN LOCATE 21,2:PRINT"***You have DIED!***                  ":GOTO 62000
61130 GOTO 10010
62000 LOCATE 23,2:LINE INPUT">Play Again?                       ";A$
62010 IF A$="Y" THEN RUN
62020 IF A$="N" THEN CLS:END
62030 CLS:END
63000 CLS
63010 PRINT"     You start to go down the ladder "
63020 PRINT" when you feel a tug from behind."
63030 PRINT" Before you know it, you're approaching"
63040 PRINT" the tractor beam housing and then..."
63050 PRINT:PRINT" ***You have DIED!***"
63060 GOTO 62000