10 'CLUB 100 LIBRARY - 415/939-1246 BBS,    937-5039 NEWSLETTER, 932-8856 VOICE
15 'converted to AMSTRAD CPC March 2019 by SRS www.cpcwiki.eu
18 DEFINT z,x,y,a,o,q
20 CLS:S=TIME/3000:FOR I=1 TO S:T=RND:NEXT I:PRINT "House Adventure":PRINT:PRINT"Remember the Imposter is last":Z=0:A=0:L=0:U=0:TN=0:T=0:DK=0:I=0:MI=0:S=0:SP=0
25 DIM O$(26),H$(6),R$(4,9),W$(3),O(26,2),R(4,9,4),H(6,2),Z1(2),A1(2),P(2)
30 O$="GOLKEYCANGLOBOOKNIBOXDIMFLADIAPILPARHAICOIBANGARBATICESHOVAS":H$="BLOVAMBEALEOMONWER":C$="GETTAKDROSAYKILSTALIGPLAREADIGINVQUION OFFBRUWAVUNLOPESPRSAVLOAUDNEWS":FOR X=0 TO 26:READ O$(X):NEXT X:FOR X=0 TO 6:READ H$(X):NEXT X
40 FOR X=0 TO 3:FOR Y=0 TO 9:READ R$(X,Y):NEXT Y,X:FOR X=0 TO 3:READ W$(X),D$(X):NEXT X:FOR X=0 TO 3:FOR Y=0 TO 9:FOR Z=0 TO 3:READ A:R(X,Y,Z)=A-1:NEXT Z,Y,X:FOR X=0 TO 5:FOR Y=0 TO 1:READ A
50 H(X,Y)=A-1:NEXT Y,X:FOR X=0 TO 26:FOR Y=0 TO 1:READ A:O(X,Y)=A-1:NEXT Y,X
60 N$=STR$(MI):H$(6)=N$:FOR X=1 TO LEN(N$):H$(6)=H$(6)+CHR$(8):NEXT X:H$(6)=H$(6)+O$(MI)
70 IF O(7,0)=99 AND A=8 THEN Z=MIN(RND*4-1,3)
80 CLS:PRINT:PRINT"YOU ARE ";R$(Z,A)
81 IF RND*50>10 THEN 90 ELSE FOR X=0 TO 19:IF O(X,0)=1 AND O(X,1)=0 THEN O(X,0)=-1:O(X,1)=-1
82 NEXT x
90 IF RND*50<40 THEN 120 ELSE FOR X=0 TO 5
100 IF H(X,O)=-1 THEN H(X,0)=MIN(RND*4-1,3):H(X,1)=mind(RND*10-1,9) ELSE IF H(X,0)<-1 THEN H(X,0)=H(X,0)+1:H(X,1)=H(X,1)+1
110 NEXT X
120 PRINT"I SEE:";:IF Z=0 AND L=0 THEN PRINT"  NOTHING--IT'S  to O DARK!":DK=DK+1:IF DK>3 THEN PRINT:PRINT"AUGHHHH . . .":PRINT:PRINT"BEWARE OF THINGS UNSEEN!!":GOTO 820 ELSE 160
130 FOR X=0 TO 26:IF O(X,0)=Z  AND O(X,1)=A THEN PRINT TAB(7);O$(X)
140 NEXT X:FOR X=0 TO 6:IF H(X,0)=Z AND H(X,1)=A THEN PRINT TAB(7);H$(X)
150 NEXT X:PRINT:PRINT"OBVIOUS EXITS ARE:";
155 FOR X=0 TO 3:IF R(Z,A,X)=-1 OR R(Z,A,X)<>INT(R(Z,A,X))THEN 156 ELSE PRINT TAB(19);D$(X)
156 NEXT X
160 IF T=20 THEN 1240 ELSE TN=TN+1:B$="":PRINT:INPUT"WHAT TO DO";A$:A$=UPPER$(A$)+" ":IF LEFT$(A$,3)="LOO"THEN 70 ELSE GOSUB 210:IF x=0 THEN PRINT"I DON'T UNDERSTAND":GOTO 160
170 ON X GOTO 260,260,350,440,510,570,590,610,660,730,790,820,830,870,910,970,1040,1100,1120,1190,1220,240,250:IF L=1 THEN U=U+1:IF U>40 THEN L=0
180 X=X-24:IF R(Z,A,X)=-1 OR R(Z,A,X)<>INT(R(Z,A,X))THEN 200
190 A=R(Z,A,X):GOTO 70
200 PRINT"YOU CAN'T GO THAT WAY!":GOTO 160
210 FOR X=1 TO LEN(A$):IF MID$(A$,X,1)=" "THEN B$=RIGHT$(A$,LEN(A$)-X) ELSE NEXT X
220 FOR Y=1 TO 63 STEP 3:IF LEFT$(A$,3)=MID$(C$,Y,3)THEN X=(Y+2)/3:Y=63:NEXT Y:RETURN
230 NEXT Y:FOR Y=64 TO 70:IF LEFT$(A$,1)=MID$(C$,Y,1)THEN X=Y-42:Y=70 ELSE X=0
231 NEXT Y:RETURN
240 IF Z=3 OR A<>9 THEN 200 ELSE Z=Z+1:GOTO 80
250 IF Z=0 OR A<>9 THEN 200 ELSE Z=Z-1:GOTO 80
260 IF B$=""THEN INPUT"TAKE WHAT";B$:B$=B$+" "
270 FOR X=0 TO 19:IF MID$(O$,X*3+1,3)=LEFT$(B$,3)THEN 280 ELSE NEXT X:PRINT"YOU CAN'T TAKE THE ";B$:GOTO 160
280 IF O(X,0)<>Z  OR O(X,1)<>A THEN IF VAL(H$(6))=X AND H(6,0)=Z AND H(6,1)=Athen PRINT:PRINT"AUGHHHH . . .":PRINT:PRINT"REMEMBER?!":PRINT:GOTO 820 ELSE PRINT"I SEE NO ";B$;"HERE":GOTO 160
290 FOR Q=0 TO 5:IF H(Q,0)=Z AND H(Q,1)=A THEN PRINT"THIS ROOM'S OCCUPANT SEEMS TO":PRINT"HAVE GROWN VERY ATTACHED TO THE":PRINT B$;"AND WON'T LET YOU HAVE IT":GOTO 160 ELSE NEXT Q
300 IF x=17 AND O(3,0)<>99 THEN PRINT"OUCH!! THAT HURTS!!--YOU CAN'T":PRINT"PICK THAT UP!":GOTO 160
310 IF x=13 AND O(6,0)<>99 THEN PRINT"DON'T BE SILLY! YOU CAN'T":PRINT"CARRY THAT MANY COINS!":GOTO 160
320 IF I>3 THEN 340 ELSE IF O(X,0)=1 AND O(X,1)=0 THEN T=T-1
330 O(X,0)=99:O(X,1)=99:I=I+1:PRINT B$"TAKEN":GOTO 160
340 PRINT"YOU CAN'T CARRY THAT MUCH,":PRINT"YOU'LL HAVE TO DROP SOMETHING":GOTO 160
350 IF B$=""THEN INPUT"DROP WHAT";B$:B$=UPPER$(B$)+" "
360 FOR X=0 TO 19:IF MID$(O$,X*3+1,3)=LEFT$(B$,3)THEN 370 ELSE NEXT X:PRINT"YOU CAN'T DROP THE "B$:GOTO 160
370 IF O(X,0)<>99 THEN PRINT"YOU HAVE NO "B$"TO DROP":GOTO 160
380 IF x=8 OR X=16 THEN L=0
390 IF x=6 AND O(13,0)=99 THEN PRINT"TRY DROPPING THE COINS FIRST!":GOTO 160
400 IF x=3 AND O(17,0)=99 THEN PRINT"YOU'LL BURN YOURSELF IF YOU":PRINT"DROP THE GLOVE NOW!":GOTO 160
410 IF x=19 AND (O(10,0)<>Z  OR O(10,1)<>A)THEN PRINT"YOU CAN'T DROP THAT--IT'LL BREAK":GOTO 160
420 O(X,0)=Z:O(X,1)=A:I=I-1:PRINT B$"DROPPED":IF Z=1 AND A=0 THEN T=T+1
430 GOTO 160
440 IF B$=""THEN INPUT"SAY WHAT";B$:B$=B$+" "
450 FOR X=0 TO 3:IF W$(X)=B$THEN 470 ELSE NEXT X
460 PRINT"NOTHING HAPPENED":GOTO 160
470 FOR Q=0 TO 1:IF Z=Z1(Q) AND A=A1(Q)THEN IF x=P(Q)THEN 490 ELSE Z=MIN(RND*4-1,3):A=MIN(9,RND*9):PRINT"YOU EXPERIENCE DISORIENTATION":GOTO 160
480 NEXT Q:GOTO 460
490 PRINT"THE AIR SHIMMERS . . .":FOR Q=0 TO 19:IF O(Q,0)=Z AND INT(O(Q,1))=Athen O(Q,1)=INT(O(Q,1)) ELSE NEXT Q
500 GOTO 160
510 IF B$=""THEN INPUT"KILL WHAT";B$:B$=UPPER$(B$)+" "
520 FOR X=0 TO 5:IF MID$(H$,X*3+1,3)=LEFT$(B$,3)THEN 530 ELSE NEXT X:PRINT"YOU CAN'T KILL THAT!":GOTO 160
530 IF H(X,0)<>Z OR H(X,1)<>Athen PRINT"I SEE NO ";B$;"HERE!":GOTO 160
540 IF O(5,0)<>99 THEN PRINT"YOU HAVE NOTHING WITH WHICH TO":PRINT"KILL THE "B$:GOTO 160
550 IF x<>4 THEN PRINT"THE "B$"WILL BECOME ANGRY":PRINT"IF YOU PERSIST!":GOTO 160
560 H(4,0)=-5:H(4,1)=-5:PRINT"THE MONK HAS BECOME FRIGHTENED":PRINT"AND RUN AWAY":GOTO 160
570 IF B$=""THEN INPUT"STAB WHAT";B$:B$=UPPER$(B$)+" "
580 GOTO 520
590 IF B$=""THEN IF L=0 THEN 830 ELSE 870
600 IF B$="ON "THEN 830 ELSE 870
610 IF B$=""THEN INPUT"PLAY WHAT";B$:B$=UPPER$(B$)+" "
620 IF LEFT$(B$,3)<>"BAN"THEN PRINT"YOU CAN'T PLAY THAT!":GOTO 160
630 IF O(14,0)<>99 THEN PRINT"YOU HAVE NO BANJO TO PLAY":GOTO 160
640 IF H(2,0)=Z AND H(2,1)=Athen H(2,0)=-5:H(2,1)=-5:PRINT"MUSIC HATH CHARM TO SOOTH THE":PRINT"SAVAGE BEAST":PRINT"THE BEAST HAS WANDARDED":PRINT"OFF IN A STATE OF BLISS!"
650 GOTO 160
660 IF B$=""THEN INPUT"READ WHAT";B$:B$=UPPER$(B$)+" "
670 FOR X=0 TO 19:IF MID$(O$,X*3+1,3)=LEFT$(B$,3)THEN 680 ELSE NEXT X:PRINT"YOU CAN'T READ THAT!":GOTO 160
680 IF O(X,0)<>99 THEN PRINT"YOU DON'T HAVE A "B$"TO READ":GOTO 160
690 IF x=4 THEN PRINT"THE WRITING IS BLURRY--IT READS:":PRINT TAB(11);"magic"CHR$(128)"words":PRINT:PRINT TAB(2);"TO MAKE OBJECTS . . . ONE OF  THE FOLLOWING.":PRINT:FOR Y=0 TO 3:PRINT TAB(10);W$(Y):NEXT Y:PRINT ELSE 710
700 PRINT TAB(2);"NOTE: BE SURE TO USE THE":PRINT TAB(8);"RIGHT WORD IN THE . . .":GOTO 160
710 IF x=11 THEN PRINT"THE PARCHMENT IS TORN--IT READS:":PRINT" . . . IS THE PLACE TO USE THEM:":PRINT TAB(7);R$(1,8):PRINT TAB(7);R$(3,0):PRINT TAB(7);R$(1,4):GOTO 160
720 PRINT"YOU CAN'T READ THAT!":GOTO 160
730 IF B$=""THEN INPUT"DIG WHAT";B$:B$=UPPER$(B$)+" "
740 IF LEFT$(B$,3)<>"DIR"THEN PRINT"YOU CAN'T DIG THE ";B$:GOTO 160
750 IF Z<>0 OR A<>0 THEN PRINT"I SEE NO DIRT TO DIG HERE!":GOTO 160
760 IF O(18,0)<>99 THEN PRINT"YOU DON'T HAVE ANYTHING TO":PRINT"DIG WITH":GOTO 160
770 PRINT"DIGGING . . .":FOR X=0 TO 19:IF O(X,0)=Z AND (INT(O(X,1))=A AND O(X,1)<>INT(O(X,1)))THEN 780 ELSE NEXT X:GOTO 160
780 O(X,1)=INT(O(X,1)):PRINT"I FOUND SOMETHING!!":GOTO 160
790 PRINT"YOU ARE PRESENTLY CARRYING:":PRINT:IF I=0 THEN PRINT TAB(7);"//NOTHING\\\\\\\\":GOTO 160
800 FOR X=0 TO 19:IF O(X,0)=99 THEN PRINT TAB(7);O$(X)
810 NEXT X:GOTO 160
820 PRINT:PRINT"YOU GOT"T"ITEMS OUT OF THE HOUSE IN"TN"MOVES":END
830 IF O(8,0)<>99 THEN PRINT"YOU HAVE NO LIGHT TO TURN ON!":GOTO 160
840 IF O(16,0)<>99 THEN PRINT"IT DOESN'T WORK":GOTO 160
850 IF U>40 THEN PRINT"THE BATTERIES ARE EXHAUSTED":GOTO 160
860 L=1:DK=0:PRINT"LIGHT ON":GOTO 160
870 IF O(8,0)<>99 THEN PRINT"YOU HAVE NO LIGHT TO TURN OFF!":GOTO 160
880 IF O(16,0)<>99 THEN PRINT"IT DOESN'T WORK ANYWAY--SO WHY":PRINT"TURN IT OFF!!":GOTO 160
890 IF U>40 THEN PRINT"THE BATTERIES ARE ALREADY DEAD!":GOTO 160
900 L=0:PRINT"LIGHT OFF":GOTO 160
910 IF B$=""THEN INPUT"BRUSH WHAT";B$:B$=UPPER$(B$)+" "
920 FOR X=0 TO 5:IF MID$(H$,X*3+1,3)=LEFT$(B$,3)THEN 930 ELSE NEXT X:PRINT"YOU CAN'T BRUSH THAT!":GOTO 160
930 IF H(X,0)<>Z OR H(X,1)<>Athen PRINT"I SEE NO "B$"HERE!":GOTO 160
940 IF O(12,0)<>99 THEN PRINT"YOU HAVE NOTHING WITH WHICH":PRINT"TO BRUSH THE ";B$:GOTO 160
950 IF x<>3 THEN PRINT"THE "B$"WILL BECOME ANGRY":PRINT"IF YOU PERSIST!!":GOTO 160
960 H(3,0)=-5:H(3,1)=-5:PRINT"PURRRRRRRRR!!!!!!!!!":PRINT:PRINT"THE LEOPARD IS VERY GRATIFIED":PRINT"FOR THE GROOMING, AND LEAVES":GOTO 160
970 IF B$=""THEN INPUT"WAVE WHAT";B$:B$=UPPER$(B$)+" "
980 FOR X=0 TO 19:IF MID$(O$,X*3+1,3)=LEFT$(B$,3)THEN 990 ELSE NEXT X:PRINT"YOU CAN'T WAVE THAT!":GOTO 160
990 IF O(X,0)<>99 THEN PRINT"YOU HAVE NO "B$"TO WAVE!":GOTO 160
1000 IF x=8 OR X=15 THEN 1010 ELSE PRINT"THAT WAS FUN!!!  BUT IT HAD":PRINT"NO EFFECT":GOTO 160
1010 IF x=8 AND H(1,0)=Z AND H(1,1)=A AND L=1 THEN PRINT"THE VAMPIRE COVERED ITS EYES":PRINT"AND FLEW AWAY!":H(1,0)=-5:H(1,1)=-5:GOTO 160
1020 IF x=15 AND H(5,0)=Z AND H(5,1)=Athen H(5,0)=-5:H(5,1)=-5:PRINT"THE WEREWOLF HOWLED AND RAN":PRINT"AWAY IN TERROR!":GOTO 160
1030 PRINT"WAVING THE "B$"HAS NO":PRINT"EFFECT":GOTO 160
1040 IF B$=""THEN INPUT"UNLOCK WHAT";B$:B$=UPPER$(B$)+" "
1050 IF LEFT$(B$,3)<>"DOO" AND LEFT$(B$,3)<>"DRA"THEN PRINT"I'M SORRY, BUT I ONLY KNOW HOW":PRINT"TO UNLOCK DOORS AND DRAWERS":GOTO 160
1060 IF LEFT$(B$,3)="DRA"THEN PRINT"SHOW ME A DRAWER AND I'LL":PRINT"OPEN IT!!":GOTO 160
1070 IF Z<>1 OR A<>1 OR O(20,0)=-1 THEN PRINT"I SEE NO DOOR THAT NEEDS":PRINT"OPENING!":GOTO 160
1080 IF O(1,0)<>99 THEN PRINT"I NEED SOMETHING FIRST!":GOTO 160
1090 O(20,0)=-1:O(20,1)=-1:R(1,1,3)=INT(R(1,1,3)):PRINT TAB(7);"<<<<C-L-I-C-K>>>>":PRINT"OK, IT'S OPEN NOW":GOTO 160
1100 IF B$=""THEN INPUT"OPEN WHAT";B$:B$=UPPER$(B$)+" "
1110 GOTO 1040
1120 IF B$=""THEN INPUT"SPRAY WHAT";B$:B$=UPPER$(B$)+" "
1130 IF O(2,0)<>99 THEN PRINT "YOU HAVE NOTHING TO SPRAY THE":PRINT B$"WITH!!":GOTO 160
1140 FOR X=0 TO 5:IF MID$(H$,X*3+1,3)=LEFT$(B$,3)THEN 1150 ELSE NEXT X:PRINT"YOU CAN'T SPRAY THAT!":GOTO 160
1150 IF H(X,0)<>Z OR H(X,1)<>Athen PRINT "I SEE NO "B$"HERE":GOTO 160
1160 IF SP>1 THEN PRINT "THE CAN IS EMPTY":GOTO 160
1170 IF x<>0 THEN SP=SP+1:PRINT"THE "B$"WILL BECOME ANGRY":PRINT"IF YOU CONTINUE TO ACT THIS WAY":GOTO 160
1180 H(0,0)=-5:H(0,1)=-5:PRINT"THROUGH THE SPRAY'S MIST, YOU":PRINT"CAN SEE THE BLOB DISAPPEAR":PRINT"DOWN A CREVICE IN THE FLOOR":GOTO 160
1190 REM DATA SAVE
1200 OPENOUT"HOUSE.DO":FOR X=0 TO 6:FOR Y=0 TO 1:PRINT#9,H(X,Y):NEXT Y,X:FOR X=0 TO 26:FOR Y=0 TO 1:PRINT#9,O(X,Y):NEXT Y,X:FOR Q=0 TO 3:FOR X=0 TO 9:FOR Y=0 TO 3:PRINT#9,R(Q,X,Y):NEXT Y,X,Q:FOR X=0 TO 1:PRINT#9,Z1(X),A1(X),P(X):NEXT X:PRINT#9,Z,A,L,U,TN,
1210 CLOSEOUT:PRINT:PRINT"THE GAME HAS BEEN SAVED UNDER UNDER FILE-NAME 'HOUSE.DO'":END
1220 REM DATA INPUT
1230 CLOSEIN:GOTO 60
1240 CLS:PRINT"CONGRATULATIONS--YOU HAVE";:PRINT"YOU HAVE SUCCESSFULLY";:PRINT"COMPLETED House Adventure":PRINT"YOU REMOVED ALL 20 OBJECTS IN";:PRINT TN"MOVES";:FOR X=0 TO 5000:NEXT X:END
1250 DATA A BAG OF GOLD,A RUSTED KEY,A CAN OF BUG SPRAY,AN OLD LEATHER GLOVE,A SORCERER'S HANDBOOK,A CARVING KNIFE,A WOODEN BOX,AN ALUMINUM DIME,A FLASHLIGHT,A SMALL DIAMOND,A SILK PILLOW,A WRINKLED PARCHMENT
1260 DATA A HAIRBRUSH,100'S OF GOLD COINS,A BANJO,A CLOVE OF GARLIC,A SET OF BATTERIES,A BLOCK OF DRY ICE,A SHOVEL,A MING VASE,A LOCKED DOOR,THE FRONT YARD,A KING SIZED BED,A BRASS BATHTUB,A SET OF STOCKS,A DUSTY MOOSE HEAD,A UNITRON 30/50 MAINFRAME
1270 DATA A PROTO PLASMIC BLOB,A VAMPIRE,A SAVAGE BEAST,A LEOPARD,AN INSANE MONK,A WEREWOLF,
1280 DATA IN A DIRT-FLOORED ROOM,IN THE LABORATORY,IN THE PUMPROOM,IN THE FURNACE ROOM,IN A DUSTY COAL BIN,IN THE TORTURE CHAMBER,IN THE WORKSHOP,IN A WALK-IN FREEZER,IN A TELEPHONE BOOTH,IN THE BASEMENT ELEVATOR
1290 DATA ON THE FRONT PORCH,IN THE FOYER,IN A BEDROOM,IN A COAT CLOSET,IN THE DINING ROOM,IN THE PANTRY,IN THE KITCHEN,IN THE FAMILY ROOM,IN A TELEPHONE BOOTH,IN THE FIRST FLOOR ELEVATOR
1300 DATA IN THE SEWING ROOM,IN A CLOSET,IN THE MASTER BEDROOM,IN A GUEST ROOM,IN A BATHROOM,IN A GUEST ROOM,IN A SITTING ROOM,IN THE DEN,IN A TELEPHONE BOOTH,IN THE SECOND FLOOR ELEVATOR
1310 DATA IN THE LIVING ROOM,IN THE LIBRARY,IN THE TROPHY ROOM,IN THE BARROOM,IN THE COMPUTER-ROOM,IN THE GAME ROOM,IN A BEDROOM,IN THE ART HALL,IN A TELEPHONE BOOTH,IN THE THIRD FLOOR ELEVATOR
1320 DATA ABRACADABRA ,NORTH,SHAZAAM ,EAST,SEERSUCKER ,WEST,UGABOOM ,SOUTH
1321 DATA ,,8,2,1,3,,,,,2,4,3,5,,10,,7,4,,9,,,7,6,8,5,,,1,7,,10,,,6,4,,,9
1322 DATA 2,,,,,8,4,1.5,,6,,8,,2,10,,8,10,,,9,,3,7,6,,,10,3,,2,5,,,,6,7,4,5,
1330 DATA ,10,,2,1,,,4,10,,,5,2,5,6,,3,6,4,7,,4,5,,5,,,8,7,,9,,,8,,,,,1,3
1331 DATA 10,3,,,,,10,3,2,,1,,,6,9,,9,,,6,5,,4,8,,8,,,6,,7,,,4,,5,,2,,1
1332 DATA 1,8,2,7,1,3,4,2,3,4,4,8
1340 DATA 1,6,1,1.5,1,3,1,9.5,4,2,2,7,2,2,2,5.5,2,4,3,2,4,8,1,8,3,7,3,9,3,8,1,1.5,4,3,4,4,4,5,3,4,2,2,2,1,3,3,3,5,1,6,3,8,4,5
1341 DATA ,8,1,4